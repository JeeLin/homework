过程也是一种抽象，他描述了对于数的符合操作，但又不依赖于特定的数。

但是在程序中，还可以将过程作为参数，或者将过程作为返回值，这样操作过程的过程称为**高阶过程**。

# 1.3.1 Procedures as Arguments
对于需要计算一个范围内对应变换后和的过程，可以抽象出基础模式：
```scheme
(define (<name> a b)
    (if (> a b)
        0
        (+ (<term> a)
           (<name> (<next> a) b))))
```
在数学中可以抽象为**求和公式**
$$\sum_{i=a}^b f(i) = f(a) + \cdots + f(b)$$
求和公式的威力在于它抽象类操作本身。

在程序中也可以按照以上的基础模式，只要将对应的占位符翻译成形式参数：
```scheme
(define (sum term a next b)
    (if (> a b)
        0
        (+ (term a)
           (sum term (next a) next b))))
```
我们使用这个高阶过程去定义一些过程：
```scheme
(define (sum term a next b)
    (if (> a b)
        0
        (+ (term a)
           (sum term (next a) next b))))

(define (inc n) (+ n 1))

(define (sum-cubes a b)
    (sum cube a inc b))

(sum-cubes 1 10)
```
```scheme
(define (sum term a next b)
    (if (> a b)
        0
        (+ (term a)
           (sum term (next a) next b))))

(define (indentity n) n)

(define (inc n) (+ n 1))

(define (sum-integers a b)
    (sum indentity a inc b))

(sum-integers 1 10)
```
$\frac{\pi}{8}\approx \frac{1}{1\times 3} +\frac{1}{5\times 7} +\frac{1}{9\times 11} + \cdots $
```scheme
(define (sum term a next b)
    (if (> a b)
        0
        (+ (term a)
           (sum term (next a) next b))))

(define (pi-sum a b)
    (define (pi-term a)
        (/ 1.0 (* a (+ a 2))))

    (define (pi-next a)
        (+ a 4))

    (sum pi-term a pi-next b))

(* 8 (pi-sum 1 10000))
```