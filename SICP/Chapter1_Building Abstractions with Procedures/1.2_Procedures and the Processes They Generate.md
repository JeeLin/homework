能够看清楚所考虑的动作的后果的能力，对于成为程序设计专家是至关重要的。

一个过程也就是一种模式，它描述了一个计算过程的局部演化方式，描述了这个计算过程的每个步骤是怎么基于前面的步骤建立起来的。

# 1.2.1 Linear Recursion and Iteration
### 递归
计算过程构造起一个推迟进行的操作所形成的链条(调用完自身后还有处理)。
```Scheme
(define (factorial n)
    (if (= n 1) 1
        (* n (factorial (- n 1)))))

(factorial 1) 
(factorial 4) 
(factorial 10)
```
递归需要解释器维护推迟执行的操作的链条，若链条长度随着n值线性增长，则称为`线性递归过程`。

使用代换模型分析，在计算(factorial 6)时，其过程为：
```Scheme
(factorial 6)
(* 6 (factorial 5))
(* 6 (* 5 (factorial 4)))
(* 6 (* 5 (* 4 (factorial 3))))
(* 6 (* 5 (* 4 (* 3 (factorial 2)))))
(* 6 (* 5 (* 4 (* 3 (* 2 (factorial 1))))))
(* 6 (* 5 (* 4 (* 3 (* 2 1)))))
(* 6 (* 5 (* 4 (* 3 2))))
(* 6 (* 5 (* 4 6)))
(* 6 (* 5 24))
(* 6 120)
720
```

## 迭代
没有任何增长(调用完自身时带入了状态信息，且可以直接返回无需再处理)
```Scheme
(define (factorial n)
    (define (factorial-iter product iter)
        (if (> iter n)
            product
            (factorial-iter (* product iter) (+ iter 1))))

(factorial-iter 1 1))
(factorial 1) 
(factorial 4) 
(factorial 10)
```
所需的计算步骤随着n线性增长，则称为`线性迭代过程`。

使用代换模型分析，在计算(factorial 6)时，其过程为：
```Scheme
(factorial 6)
(fact-iter 1 1 6)
(fact-iter 1 2 6)
(fact-iter 2 3 6)
(fact-iter 6 4 6)
(fact-iter 24 5 6)
(fact-iter 120 6 6)
(fact-iter 720 7 6)
720
```

# 1.2.2 Tree Recursion
## 斐波那契(Fibonacci)数
### 递归方式
```Scheme
(define (fib n)
    (cond ((= n 0) 0)
          ((= n 1) 1)
          (else (+ (fib (- n 1))
                   (fib (- n 2))))))
(fib 2)
(fib 4)
(fib 10)
(fib 20)
; fib(n) -> fib(n-2) -> ...
;        -> fib(n-1) -> fib(n-3) -> ...
;                    -> fib(n-2) -> fib(n-4) -> ...
;                                -> fib(n-3) -> ... -> fib(2) -> fib(0)
;                                                             -> fib(1)
```
一般来说，树形递归的计算步骤步骤正比于树中的节点数，空间需求正比于树的最大深度。

### 迭代方式
将递归返回的操作数使用变量存储
```Scheme
(define (fib n)
    (define (fib-iter next current n)
        (cond ((= n 0) current)
              (else (fib-iter (+ current next) next (- n 1)))))

    (fib-iter 1 0 n)); (f f(1) f(0) n)
(fib 2)
(fib 4)
(fib 10)
(fib 20)
```

### Example: Counting change
1美元换50、25、10、5、1美分。有多少种方式？
```Scheme
(define (count-charge amount)
    (define (cc amount kinds)
        (cond ((= amount 0) 1)
              ((or (< amount 0) (< kinds 1)) 0)
              (else (+ (cc amount (- kinds 1))
                       (cc (- amount (max-change kinds)) kinds)))))

    (define (max-change kinds)
        (cond ((= kinds 5) 50)
              ((= kinds 4) 25)
              ((= kinds 3) 10)
              ((= kinds 2) 5)
              ((= kinds 1) 1)))
    
    (cc amount 5))
(count-charge 100)
```
树形递归易于理解，但效率上可能极其低效。


# 1.2.3 Orders of Growth
如果存在与n无关的整数 $k_1$和 $k_2$，使得
$$k_1f(n) \leqslant R(n) \leqslant k_2R(n)$$
对于任何足够大的n值都成立，则我们称 $R(n)$具有 $\Theta(f(n))$的增长阶，记作 $R(n)=\Theta(f(n))$。
> $\exists k_1,k_2,N \in N^+, \forall n>N, k_1f(n)\leqslant R(n)\leqslant k_2R(n)$

> $\exists k_1,k_2\in N^+, k_1\leqslant\lim\limits_{x\rightarrow\infty}R(n)\leqslant k_2$

线性递归中，计算过程所需步骤的增长为 $\Theta(n)$，空间需求的增长也是 $\Theta(n)$。
线性递归中，计算过程所需步骤的增长为 $\Theta(n)$，空间需求的增长也是 $\Theta(1)$。

## 时间复杂度
> 也就是估算程序中执行最多的命令的执行次数。
1. 循环次数最多原则：只需关注循环次数最多的那段代码
2. 加法原则：量级最大的那段代码时间复杂度=总的时间复杂度
3. 乘法原则：嵌套代码的复杂度等于嵌套内外代码复杂度的乘积

## 空间复杂度
1. 常量空间：存储空间大小固定，和输入没有关系时，空间复杂度是O(1)
2. 线性空间：算法中定义了一个线性集合，如一个列表，并且集合大小和输入规模n成正比，空间复杂度记为O(n)
3. 二维空间：算法中定义了一个二维列表集合，并且集合的长和宽都和输入规模n成正比，空间复杂度记为O(nn)/O(nm)
4. 递归空间：递归过程就是一个进栈和出栈的过程，当进入一个新函数时，进行入栈操作，把调用的函数和参数信息压入栈中；当函数返回时，执行出栈。
递归的空间复杂度也是线性的，如果递归的深度是n，那么空间复杂度就是O(n)。

# 1.2.4 Exponentiation
(仅用于b,n > 0)
## 递归
```scheme
(define (expt b n)
    (if (= n 0)
        1
        (* b (expt b (- n 1)))))
```
## 迭代
```scheme
(define (expt b n)
    (define (expt-iter b n product)
        (if (= n 0)
            product
            (expt-iter b (- n 1) (* b product))))
    
    (expt-iter b n 1))
```

## 使用乘法简化求幂计算
```scheme
(define (fast-expt b n)
    (define (even? n)
        (= 0 (remainder n 2)))
    
    (define (square x) (* x x))

    (cond ((= n 0) 1)
          ((even? n) (square (fast-expt b (/ n 2))))
          (else (* b (fast-expt b (- n 1))))))
```

# 1.2.5 Greatest Common Divisors
## Euclid’s Algorithm
> 如果r是a除以b的余数，那么a和b的公约数正好也是b和r的公约数。[证明过程](https://zhuanlan.zhihu.com/p/346479426)

欧几里得算法计算最大公约数
```scheme
(define (gcd a b)
    (if (= b 0)
        a
        (gcd b (remainder a b))))
```

## Lamé’s Theorem
> 如果欧几里得算法需要用k步计算出一对整数的GCD,那么这对数中较小的那个数必然大于或者等于第k个斐波那契数。

# 1.2.6 Example: Testing for Primality
## Searching for divisors
$\Theta(\sqrt{n})$
```scheme
(define (smallest-divisor n)
  (find-divisor n 2))

(define (find-divisor n test-divisor)
  (cond ((> (square test-divisor) n) 
         n)
        ((divides? test-divisor n) 
         test-divisor)
        (else (find-divisor 
               n 
               (+ test-divisor 1)))))

(define (divides? a b)
  (= (remainder b a) 0))
```

## The Fermat test
> 费马小定理：p为质数，a为任意自然数，则 $a^p \equiv a(mod p)$。[证明过程](https://zhuanlan.zhihu.com/p/87611586)

其逆命题不成立。且如果n不是质数，一般而言，大多数a<n都将满足以上关系。因此有以下检测质数的算法：
> $\exists n \in Z^+, \forall a \lt n, if\ a^n(mod n) \ne a,\ then\ n\ is\ not\ prime$

否则n有概率是质数。

$\Theta(logn)$
```scheme
(define (expmod base exp m)
    (define (even? n)
        (= 0 (remainder n 2)))

    (define (square x) (* x x))
    
    (cond ((= exp 0) 1)
          ((even? exp) (remainder (square (expmod base (/ exp 2) m)) m))
          (else (remainder (* base (expmod base (- exp 1) m)) m))))

(define (fermat-test n)
    (define (try-it a)
        (= (expmod a n n) a))
    
    (try-it (+ 1 (random (- n 1)))))

(define (fast-prime? n times)
    (cond ((= times 0) true)
          ((fermat-test n) (fast-prime? n (- times 1)))
          (else false)))
```
## Probabilistic methods
以上费马检测的输出实际上是概率上的正确性。

如果数n无法通过费马检测，则其一定不是质数；但能通过只能说明其``很可能``是质数。但是可以通过更多随机的a值去运行这一检查，可以将出现错误的概率降到所需要的任意程度。